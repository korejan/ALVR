plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.alvr.alxr_client'
    compileSdk 34
    ndkVersion '27.0.11718014'
    buildToolsVersion '34.0.0'

    defaultConfig {
        //applicationId "com.alvr.alxr_client"
        minSdk 29 // to enable official low-latency api mode, minsdk level must be >= 30
        targetSdk 32
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            cmake {
                cFlags "-flto=thin"
                cppFlags "-std=c++20 -fexceptions -frtti -flto=thin"
                arguments "-DANDROID_TOOLCHAIN=clang", "-DANDROID_STL=c++_static", "-DANDROID_ARM_NEON=TRUE",
                        "-DBUILD_API_LAYERS=OFF", "-DBUILD_TESTS=OFF", "-DBUILD_CONFORMANCE_TESTS=OFF", '-DBUILD_ALL_EXTENSIONS=ON'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    externalNativeBuild {
        cmake {
            path file('../../cpp/ALVR-OpenXR-Engine/CMakeLists.txt')
            version '3.22.1'
        }
    }

    flavorDimensions 'headset'
    productFlavors {
        Generic {
            dimension 'headset'
            ndk {
                abiFilters = project.hasProperty('ABI') ? project.ABI.split(',') : ['arm64-v8a', 'armeabi-v7a', 'x86_64', 'x86']
            }
            externalNativeBuild.cmake.arguments += ["-DBUILD_LOADER=ON"]
        }
        PicoMobileOXR {
            dimension 'headset'
            ndk {
                abiFilters 'arm64-v8a'
            }
            externalNativeBuild.cmake.arguments += ["-DBUILD_LOADER=OFF", "-DUSE_PICO_MOBILE_LOADER=ON"]
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
}